plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.basistheory.android.example'
    compileSdk 32

    defaultConfig {
        applicationId 'com.basistheory.android.example'
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'basisTheoryApiUrl', tryFindProperty('com.basistheory.android.example.apiUrl')
        buildConfigField 'String', 'basisTheoryApiKey', tryFindProperty('com.basistheory.android.example.apiKey')

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(':lib')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'org.threeten:threetenbp:1.6.4'

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.github.javafaker:javafaker:1.0.2'
}

/**
 * Searches for the value of a gradle property defined with the given name, preferring values
 * defined in local.properties, if available.
 * <p>
 * Note: buildConfigField requires a string value that is entered literally into an autogenerated source
 * file. Strings must be wrapped in quotes to be rendered as [String property = "value"] and the
 * string "null" is rendered as [String property = null] in the autogenerated BuildConfig file.
 */
String tryFindProperty(String name) {
    var localProperties = tryReadLocalProperties()
    var propertyValue = localProperties[name] ?: findProperty(name)

    return propertyValue != null
            ? "\"${propertyValue}\""
            : "null"
}

Properties tryReadLocalProperties() {
    def properties = new Properties()

    try {
        def propFile = rootProject.file("./local.properties")
        properties.load(new FileInputStream(propFile))
    } catch (ignored) {
    }

    return properties
}
