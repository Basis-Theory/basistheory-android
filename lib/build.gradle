plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.basistheory.android'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        versionName = '2.4.0'

        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                    exceptionFormat "full"
                }
            }
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.basistheory'
                artifactId 'basistheory-android'
                version = android.defaultConfig.versionName
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.2'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.threeten:threetenbp:1.6.8'
    implementation 'com.github.basis-theory:basistheory-java:0.5.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'io.strikt:strikt-core:0.34.1'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation 'io.mockk:mockk:1.13.5'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.11.0")
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}

configurations.all {
    resolutionStrategy {
        force 'org.yaml:snakeyaml:2.1'
    }
}